apply plugin: 'maven-publish'

android.libraryVariants.all { variant ->
    task("generate${variant.name.capitalize()}SourcesJar", type: Jar) {
        group 'Sources'
        description "Generates sources jar for $variant.name build."
        classifier = "${variant.name}-sources"
        Closure sum = { set, i -> set + i }
        into('assets') { from variant.sourceSets*.assetsDirectories.inject([], sum) }
        into('res') { from variant.sourceSets*.resDirectories.inject([], sum) }
        into('rs') { from variant.sourceSets*.renderscriptDirectories.inject([], sum) }
        from variant.sourceSets*.aidlDirectories.inject([], sum)
        from variant.sourceSets*.javaDirectories.inject([], sum)
        from variant.sourceSets*.resourcesDirectories.inject([], sum)
        from variant.sourceSets*.manifestFile
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact bundleRelease
            artifact generateReleaseSourcesJar

            pom.withXml {
                asNode().appendNode('name', project.ext.name)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', project.ext.website)
                asNode().children().last() + project.ext.pom
            }
        }
    }
}

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.6' }
}
// hack: plugin 'com.jfrog.bintray' is not visible by name
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin
// hack: without it bintray property is not visible
logger.debug("$project.ext.bintray")

bintray {
    user = project.ext.bintray.user
    key = project.ext.bintray.key
    publications = ['maven']
    dryRun = project.ext.bintray.dryRun
    publish = project.ext.bintray.publish
    pkg {
        repo = project.ext.bintray.repo
        name = project.name
        desc = project.description
        websiteUrl = project.ext.website
        issueTrackerUrl = project.ext.issueTracker
        vcsUrl = project.ext.vcs
        licenses = project.ext.bintray.licenses
        labels = project.ext.bintray.tags
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}
